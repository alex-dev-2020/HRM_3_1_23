
// только измененные Процедуры

Процедура ЗаполнитьЗначенияДополнительныхПоказателейПоТаблицеЗначений(ДополнительныеПоказатели, ЗначенияПоказателей, МенеджерРасчета) Экспорт
	
	Показатель = ЗарплатаКадрыРасширенный.ПоказательПоИдентификатору("_КоличествоЧасовВыполненияВредныхРабот");
	
	Если Показатель =  Неопределено Тогда	
		Возврат;		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДополнительныеПоказатели.Показатель КАК Показатель,
	|	ДополнительныеПоказатели.Сотрудник КАК Сотрудник,
	|	ДополнительныеПоказатели.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ДополнительныеПоказатели.ДатаНачала КАК ДатаНачала
	|ПОМЕСТИТЬ ВТДополнительныеПоказатели
	|ИЗ
	|	&ДополнительныеПоказатели КАК ДополнительныеПоказатели
	|ГДЕ
	|	ДополнительныеПоказатели.Показатель = &Показатель
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТДополнительныеПоказатели.Показатель КАК Показатель,
	|	ВТДополнительныеПоказатели.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	СУММА(ЕСТЬNULL(ДанныеОперативногоУчетаРабочегоВремениСотрудников.Часы, 0)) КАК Значение
	|ИЗ
	|	РегистрНакопления.ДанныеОперативногоУчетаРабочегоВремениСотрудников КАК ДанныеОперативногоУчетаРабочегоВремениСотрудников
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДополнительныеПоказатели КАК ВТДополнительныеПоказатели
	|		ПО ДанныеОперативногоУчетаРабочегоВремениСотрудников.Сотрудник = ВТДополнительныеПоказатели.Сотрудник
	|			И ДанныеОперативногоУчетаРабочегоВремениСотрудников.Период < ВТДополнительныеПоказатели.ДатаНачала
	|ГДЕ
	|	ДанныеОперативногоУчетаРабочегоВремениСотрудников.ВидУчетаВремени = &РаботаВЛаборатории
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТДополнительныеПоказатели.Показатель,
	|	ВТДополнительныеПоказатели.ИдентификаторСтроки";
	
	Запрос.УстановитьПараметр("ДополнительныеПоказатели", ДополнительныеПоказатели);
	Запрос.УстановитьПараметр("Показатель", Показатель);
	Запрос.УстановитьПараметр("РаботаВЛаборатории", 
	Справочники.ВидыИспользованияРабочегоВремени.НайтиПоРеквизиту("БуквенныйКод", "РЛ"));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		СтрокаПоказателя = ЗначенияПоказателей.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаПоказателя,ВыборкаДетальныеЗаписи);
		
	КонецЦикла;

	
КонецПроцедуры


Процедура ПриОпределенииДополнительныхПоказателей(ДополнительныеПоказатели) Экспорт
	
	Показатель = ЗарплатаКадрыРасширенный.ПоказательПоИдентификатору("_КоличествоЧасовВыполненияВредныхРабот");
	
	Если Показатель <> Неопределено Тогда
		
		ДополнительныеПоказатели.Добавить(Показатель);
			
	КонецЕсли
	
КонецПроцедуры
