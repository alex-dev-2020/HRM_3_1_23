

// +++ 
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// Проведение документа
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	Движения.Записать();
	
	
	ДанныеДляПроведения = ПолучитьДанныеДляПроведения(); 
	
	РасчетЗарплаты.СформироватьДвиженияПлановыхНачислений(ЭтотОбъект, Движения, ДанныеДляПроведения);
	
	
	
КонецПроцедуры      

Функция  ПолучитьДанныеДляПроведения()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ГрафикРаботыССекретностьюРаботаССекретностью.Ссылка.Организация КАК ГоловнаяОрганизация,
	|	ГрафикРаботыССекретностьюРаботаССекретностью.Дата КАК ДатаСобытия,
	|	ЗНАЧЕНИЕ(Перечисление.ДействияСНачислениямиИУдержаниями.Утвердить) КАК Действие,
	|	ДОБАВИТЬКДАТЕ(ГрафикРаботыССекретностьюРаботаССекретностью.Дата, ДЕНЬ, 1) КАК ДействуетДо,
	|	ГрафикРаботыССекретностьюРаботаССекретностью.Ссылка.Ссылка КАК ДокументОснование,
	|	ИСТИНА КАК Используется,
	|	ЛОЖЬ КАК ИспользуетсяПоОкончании,
	|	ГрафикРаботыССекретностьюРаботаССекретностью.Ссылка.Начисление КАК Начисление,
	|	ГрафикРаботыССекретностьюРаботаССекретностью.Сотрудник КАК Сотрудник,
	|	ГрафикРаботыССекретностьюРаботаССекретностью.Сотрудник.ФизическоеЛицо КАК ФизическоеЛицо
	|ИЗ
	|	Документ.ГрафикРаботыССекретностью.РаботаССекретностью КАК ГрафикРаботыССекретностьюРаботаССекретностью
	|ГДЕ
	|	ГрафикРаботыССекретностьюРаботаССекретностью.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикРаботыССекретностьюРаботаССекретностью.Ссылка.Организация КАК Организация,
	|	ГрафикРаботыССекретностьюРаботаССекретностью.Дата КАК ДатаСобытия,
	|	ЗНАЧЕНИЕ(Перечисление.ДействияСНачислениямиИУдержаниями.Утвердить) КАК Действие,
	|	ДОБАВИТЬКДАТЕ(ГрафикРаботыССекретностьюРаботаССекретностью.Дата, ДЕНЬ, 1) КАК ДействуетДо,
	|	ГрафикРаботыССекретностьюРаботаССекретностью.Ссылка.Ссылка КАК ДокументОснование,
	|	ГрафикРаботыССекретностьюРаботаССекретностью.Сотрудник КАК Сотрудник,
	|	ГрафикРаботыССекретностьюРаботаССекретностью.Сотрудник.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ГрафикРаботыССекретностьюРаботаССекретностью.Ссылка.Показатель КАК Показатель,
	|	ЕСТЬNULL(ПроцентыНадбавокЗаСекретность.ПроцентНадбавки, 0) КАК Значение
	|ИЗ
	|	Документ.ГрафикРаботыССекретностью.РаботаССекретностью КАК ГрафикРаботыССекретностьюРаботаССекретностью
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПроцентыНадбавокЗаСекретность КАК ПроцентыНадбавокЗаСекретность
	|		ПО ГрафикРаботыССекретностьюРаботаССекретностью.СтепеньСекретности = ПроцентыНадбавокЗаСекретность.СтепеньСекретности
	|ГДЕ
	|	ГрафикРаботыССекретностьюРаботаССекретностью.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет(); 
	
	ДанныеДляПроведения = Новый Структура;
	
	ДанныеДляПроведения.Вставить("ДанныеОПлановыхНачислениях",РезультатЗапроса[0].Выгрузить());
	ДанныеДляПроведения.Вставить("ЗначенияПоказателей",РезультатЗапроса[1].Выгрузить()); 
	
	Возврат ДанныеДляПроведения;
	
	
КонецФункции

// --- 
